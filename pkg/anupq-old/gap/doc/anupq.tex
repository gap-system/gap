%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%
%A  anupq.tex                GAP documentation                 Eamonn O'Brien
%A                                                             & Frank Celler
%A                                                           & Benedikt Rothe
%%
%A  @(#)$Id: anupq.tex,v 1.1.1.1 2001/04/15 13:39:19 werner Exp $
%%
%%

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\Chapter{The ANU pq in GAP 4}

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

Using the ANU PQ share package, the ANU $p$-quotient program (ANU pq) may
be called from {\GAP}.  Using this program, {\GAP} provides access to the
following:   the  $p$-quotient   algorithm;   the  $p$-group   generation
algorithm; a standard presentation algorithm.

The following section  describes the functions which gives  access to the
$p$-quotient algorithm.

The  next  section describes  the  function  which  gives access  to  the
$p$-group generation algorithm.

The  next  section describes  the  function  which  gives access  to  the
standard presentation algorithm.

The last sections describes  the function which implements an isomorphism
test for $p$-groups using the standard presentation algorithm.

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\Section{Computing p-Quotients}

\>Pq( <F>, ... )

Let <F> be a  finitely presented  group.  Then  'Pq' returns  the desired
$p$-quotient of <F> as a pc-group.

The following parameters or parameter pairs are supported.

\beginitems
\"Prime\", <p> & 
    Compute the $p$-quotient for the prime <p>.

\"ClassBound\", <n> & 
    The $p$-quotient computed has lower exponent-$p$ class at most <n>.    

\"Exponent\", <n> & 
    The $p$-quotient computed has exponent <n>. By default, no
    exponent law is enforced. 

\"Metabelian\" & 
    The largest metabelian $p$-quotient is constructed. 

\"Verbose\" &
    The runtime-information generated  by  the ANU  pq is  displayed.  By
    default, pq works silently.

\"OutputLevel\", <n> &
    The runtime-information  generated by the  ANU  pq  is  displayed  at
    output  level  <n>,  which  must  be a  integer from  0 to  3.   This
    parameter implies \"Verbose\".

\"SetupFile\", <name> &
    Do not run the ANU pq, just  construct the input file and store it in
    the file <name>. In this case 'true' is returned.
\enditems

Alternatively, you can pass 'Pq' a record as a parameter, which  contains
as entries some (or all)  of the above mentioned.  Those parameters which
do not occur in the record are set to their default values.

See also "PqEpimorphism".

\beginexample
    gap> RequirePackage("anupq");
    gap> f2 := FreeGroup( 2, "f2" );
    <free group on the generators [ f21, f22 ]>
    gap> Pq( f2, rec( Prime := 2,  ClassBound := 3 ) );
    <pc group of size 1024 with 10 generators>
    gap> g := f2 / [ f2.1^4, f2.2^4 ];;
    gap> Pq( g, rec( Prime := 2, ClassBound := 3 ) );
    <pc group of size 256 with 8 generators>
    gap> Pq( g, "Prime", 2, "ClassBound", 3, "Exponent", 4 );
    <pc group of size 128 with 7 generators>
    gap> g := f2 / [ f2.1^25, Comm(Comm(f2.2,f2.1),f2.1), f2.2^5 ];;
    gap> Pq( g, "Prime", 5, "Metabelian", "ClassBound", 5 );
    <pc group of size 78125 with 7 generators>
\endexample

This function requires the package \"anupq\"\ (see `RequirePackage').

\>PqEpimorphism( ... )

This function takes the same parameters as 'Pq'.  It returns an
epimorphism from the finitely presented group onto the computed
p-quotient.

\beginexample
    gap> F := FreeGroup (2, "F");
    <free group on the generators [ F1, F2 ]>
    gap>  phi := PqEpimorphism( F, "Prime", 5, "Class", 2);
    [ F1, F2 ] -> [ f1, f2 ]
    gap> Image( phi );
    <pc group of size 3125 with 5 generators>
\endexample

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\Section{Computing Descendants of a p-Group}

\>PqDescendants( <G>, ... )

Let  <G>  be  an  pc-group   of  prime  power  order  with  a  consistent
power-commutator  presentation   (see  `IsConsistent').   'PqDescendants'
returns a list of descendants of <G>.

If <G>  does *not* have $p$-class  1, then the automorphism  group of <G>
must be  known.  In practice, the  automorphism group of  <G> is computed
using the {\GAP} command `AutomorphismGroup'.  Note that the {\GAP} share
package `AUTPGRP'  provides an  algorithm for computing  the automorphism
group of  a $p$-group  which perform better  than the standard  method in
{\GAP}.

If  the automorphism  group of  <G> is  not soluble  or if  the parameter
\"PcgsAutomorphisms\" (see below) is not  used, the ANU pq will call {\GAP}
together  with   teh  AUTPGRP  package   as  a  subprocess   for  certain
orbit-stabilizer calculations.

The following optional parameters or parameter pairs are supported.

\beginitems
\"ClassBound\", <n> &
    'PqDescendants' generates  only  descendants  with lower exponent-$p$
    class  at most  <n>.  The default value is  the exponent-$p$ class of
    <G> plus one.

\"OrderBound\", <n> &
    'PqDescendants' generates only descendants of  size at  most $p^<n>$.
    Note that you cannot set both \"OrderBound\"\ and \"StepSize\".

\"StepSize\", <n> &
    Let <n> be a  positive integer.  'PqDescendants' generates only those
    immediate  descendants  which are $p^<n>$  bigger  than  their parent
    group.

\"StepSize\", <l> &
    Let <l> be a  list of  positive  integers such that  the  sum  of the
    length of <l> and the exponent-$p$ class of <G> is equal to the class
    bound \"ClassBound\".  Then  <l> describes  the  step size  for  each
    additional class.

\"PcgsAutomorphisms\" &
    This  parameter  instructs  the  function  to  compute  a  polycyclic
    generating sequence for the automorphism group of <G> and to pass the
    polycyclic  generating sequence to  the ANU  pq.  This  increases the
    efficiency  of the  computation.  It  also prevents  the ANU  pq from
    calling GAP as a subprocess for orbit-stabilizer calculations.

    This parameter can  be used only if the automorphism  group of <G> is
    soluble.  If this parameter is  used in conjunction with an insoluble
    automorphism group, an error message occurs.

\"RankInitialSegmentSubgroups\", <n> &
    Set the rank  of the  initial  segment  subgroup chosen to  be <n>.
    By default, this has value 0.

\"SpaceEfficient\" &
    The ANU  pq performs  calculations  more slowly but with greater space
    efficiency.
    This  flag is  frequently  necessary  for  groups of  large  Frattini
    quotient rank.  The space  saving occurs because only one permutation
    is  stored at  any  one  time.   This option  is  only  available  in
    conjunction with the \"PcgsAutomorphisms\"\ flag.

\"AllDescendants\" &
    By default, only capable descendants are constructed. If this flag
    is set, compute all descendants.

\"Exponent\", <n> &
    Construct only descendants with exponent <n>.  Default is no exponent
    law.

\"Metabelian\" &
    Construct only metabelian descendants.

\"SubList\", <sub> &
    Let $L$  be  the  list of  descendants  generated.  If  list <sub> is
    supplied,  'PqDescendants'  returns  'Sublist( $L$,<sub> )'.  If   an
    integer <n> is supplied, 'PqDescendants' returns $L[<n>]$.

\"Verbose\" &
    The runtime-information  generated by  the  ANU pq is  displayed.  By
    default, pq works silently.

\"SetupFile\", <name> &
    Do not run the ANU pq, just construct  the input file and store it in
    the file <name>. In this case 'true' is returned.

\"TmpDir\", <dir> &
    'PqDescendants' stores intermediate results  in temporary  files; the
    location  of  these  files  is  determined  by the value  selected by
    'TmpName'.  If your default temporary directory does not  have enough
    free disk space,  you can supply an alternative path  <dir>.  In this
    case 'PqDescendants' stores its intermediate results  in  a temporary
    subdirectory of <dir>.
    Alternatively, you can globally set  the  variable 'ANUPQtmpDir', for
    instance in your \".gaprc\"\ file, to point to a suitable location.
\enditems

Alternatively,  you can pass 'PqDescendants'  a record  as  a  parameter,
which  contains  as  entries some (or all) of the above mentioned.  Those
parameters  which do not occur  in  the record are  set  to their default
values.

Note that you cannot set both \"OrderBound\"\ and \"StepSize\".

In the first example  we  compute all descendants of the Klein four group
which have exponent-2 class at most 5 and order at most $2^6$.

\beginexample
    gap> f2 := FreeGroup( 2, "g" );;                                       
    gap> G := PcGroupFpGroup(f2 / [f2.1^2, f2.2^2, Comm(f2.2,f2.1)]);
    <pc group of size 4 with 2 generators>
    gap> l := PqDescendants( G, "OrderBound", 6, "ClassBound", 5,
                                "AllDescendants" );
    gap>  Length(l);
    83
    gap>  List( l, x -> Size(x) );
    [ 8, 8, 8, 16, 16, 16, 32, 16, 16, 16, 16, 16, 32, 32, 64, 64, 32, 32, 
      32, 32, 32, 32, 32, 64, 64, 64, 64, 64, 64, 64, 64, 64, 64, 64, 32,
      32, 32, 32, 64, 64, 64, 64, 64, 64, 64, 64, 64, 64, 64, 32, 32, 32,
      32, 32, 64, 64, 64, 64, 64, 64, 64, 64, 64, 64, 64, 64, 64, 64, 64,
      64, 64, 64, 64, 64, 64, 64, 64, 64, 64, 64, 64, 64, 64 ]
    gap>  List( l, x -> Length( PCentralSeries( x, 2 ) ) - 1 );
    [ 2, 2, 2, 2, 2, 2, 2, 3, 3, 3, 3, 3, 3, 3, 3, 3, 3, 3, 3, 3, 3, 3, 3,
      3, 3, 3, 3, 3, 3, 3, 3, 3, 3, 3, 3, 3, 3, 3, 3, 3, 3, 3, 3, 3, 3, 3,
      3, 3, 3, 4, 4, 4, 4, 4, 4, 4, 4, 4, 4, 4, 4, 4, 4, 4, 4, 4, 4, 4, 4,
      4, 4, 4, 4, 4, 4, 4, 4, 4, 5, 5, 5, 5, 5 ]
\endexample

In the second example we compute all  capable descendants of order  27 of
the  elementary abelian group of order 9.  

\beginexample
    gap> f2 := FreeGroup( 2, "g" );;                                       
    gap> G := PcGroupFpGroup(f2 / [ f2.1^3, f2.2^3, Comm(f2.1,f2.2) ]);
    <pc group of size 9 with 2 generators>
    gap> A := AutomorphismGroup( G );
    <group with 4 generators>
    gap> IsSolvable(A);                                       
    true
    gap> Pcgs(A);
    Pcgs([ [ g1, g2 ] -> [ g1, g2^2 ], [ g1, g2 ] -> [ g1, g1*g2 ], 
      [ g1, g2 ] -> [ g1^2*g2, g1*g2 ], [ g1, g2 ] -> [ g2^2, g1 ], 
      [ g1, g2 ] -> [ g1^2, g2^2 ] ])
    gap>  l := PqDescendants( G, "OrderBound", 3,
    >                            "ClassBound", 2,
    >                            "PcgsAutomorphisms" );;
    gap>  Length(l);
    2
    gap> List( l, x -> Size(x) );
    [ 27, 27 ]
    gap> List( l, x -> Length( PCentralSeries( x, 3 ) ) - 1 );
    [ 2, 2 ]
\endexample

In  the  third  example,  we  compute  all  capable  descendants  of  the
elementary abelian group of order  $5^2$ which have exponent-$5$ class at
most $3$, exponent $5$, and are metabelian.

\beginexample
    gap> f2 := FreeGroup( 2, "g" );;
    gap> G := PcGroupFpGroup(f2 /  [f2.1^5, f2.2^5, Comm(f2.2,f2.1)] );
    <pc group of size 25 with 2 generators>
    gap> l := PqDescendants(G,"Metabelian","ClassBound",3,"Exponent",5);;
    gap> List( l, x -> Length( PCentralSeries( x, 5 ) ) - 1 );
    [ 2, 3, 3 ]
    gap>  List( l, x -> Length( DerivedSeries( x ) ) );
    [ 3, 3, 3 ]
    gap> List( l, x -> Maximum( List( Elements(x), Order ) ) );  
    [ 5, 5, 5 ]
\endexample

This function requires the package \"anupq\"\ (see `RequirePackage').

\>PqList( <file> )
\>PqList( <file>, <sub> )
\>PqList( <file>, <n> )

The function 'PqList' reads a file  <file> and returns the list $L$ of ag
groups defined in this file.

If list <sub> is supplied as a parameter, the function  returns 'Sublist(
$L$, <sub> )'.  If an integer <n> is supplied, 'PqList' returns $L[<n>]$.

This  function  and  'SavePqList'  (see  "SavePqList")  can  be  used  to
save and restore a list of descendants (see "PqDescendants").

This function requires the package \"anupq\"\ (see `RequirePackage').

\>SavePqList( <name>, <list> )

The function 'SavePqList' writes a list of  descendants  <list> to a file
<name>.

This function and 'PqList' (see "PqList") can be used to save and restore
results of 'PqDescendants' (see "PqDescendants").

This function requires the package \"anupq\"\ (see `RequirePackage').

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\Section{Computing Standard Presentations}
\index{automorphisms!of p groups}

\>StandardPresentation( <F>, <p>, ...  )
\>StandardPresentation( <F>, <G>, ...  )

Let  <F> be  a  finitely  presented group.   Then  'StandardPresentation'
returns the standard presentation for the  desired $p$-quotient of <F> as
a finitely presented group.

A  finitely-presented group <F> must be  supplied  as input. Usually, the
user will  also  supply a prime  <p> and  the  program will  compute  the
standard presentation for the desired $p$-quotient of <F>.

Alternatively, a user  may supply a pc group <G> which  is the class 1
$p$-quotient of <F>. If this is so, the automorphism group of <G> must
be known.  The  presentation for <G> can be  constructed by an initial
call to Pq (see "Pq").

Of course, <G> need not be  the class 1 $p$-quotient of <F>.  However,
the automorphism group  of <G> must be known and  this is most readily
available  when  <G>  is  an  elementary  abelian  group.   Where  the
necessary information is available for a $p$-quotient of higher class,
one  can apply  the standard  presentation algorithm  from  that class
onwards.

The following parameters or parameter pairs are supported.

\beginitems
\"ClassBound\", <n> &
    The  standard presentation is  computed for  the largest $p$-quotient
    of <F> having lower exponent-$p$ class at most <n>.

\"Exponent\", <n> &
    The $p$-quotient computed has  exponent <n>.  By default, no exponent
    law is enforced.

\"Metabelian\" &
    The $p$-quotient constructed is metabelian.

\"PcgsAutomorphisms\" &
    This  parameter  instructs  the  function  to  compute  a  polycyclic
    generating sequence for the automorphism group of <G> and to pass the
    polycyclic  generating sequence to  the ANU  pq.  This  increases the
    efficiency  of the  computation.  It  also prevents  the ANU  pq from
    calling GAP  as a subprocess for  orbit-stabilizer calculations.  See
    section   "Computing   Descendants   of   a  p-Group"   for   further
    explanations.

    This parameter can  be used only if the automorphism  group of <G> is
    soluble.  If this parameter is  used in conjunction with an insoluble
    automorphism group, an error message occurs.

\"Verbose\" &
    The  runtime-information generated by  the  ANU  pq is displayed.  By
    default, pq works silently.

\"OutputLevel\", <n> &
    The  runtime-information generated  by  the  ANU pq is  displayed  at
    output level  <n>, which  must  be  a integer  from  0  to  3.   This
    parameter implies \"Verbose\".

\"SetupFile\", <name> &
    Do not run the ANU pq, just construct the input file and  store it in
    the file <name>. In this case 'true' is returned.

\"TmpDir\", <dir> &
    'StandardPresentation'  stores  intermediate  results  in   temporary
    files;  the location  of  these  files  is  determined by  the  value
    selected by 'TmpName'.  If your default  temporary directory does not
    have enough  free  disk  space,  you  can supply an  alternative path
    <dir>.  In this case  'StandardPresentation' stores its  intermediate
    results in a temporary subdirectory of <dir>.  Alternatively, you can
    globally  set  the  variable  'ANUPQtmpDir',  for  instance  in  your
    \".gaprc\"\ file, to point to a suitable location.
\enditems

Alternatively,  you  can  pass   'StandardPresentation'  a  record  as  a
parameter,  which  contains  as  entries  some  (or  all)  of  the  above
mentioned.  Those parameters which do not occur in the record are set  to
their default values.

We illustrate the method with the following examples.

\beginexample
    gap> f2 := FreeGroup( "a", "b" );;
    gap> g := f2 / [f2.1^25, Comm(Comm(f2.2,f2.1), f2.1), f2.2^5];
    <fp group on the generators [ a, b ]>
    gap> StandardPresentation( g, 5, "ClassBound", 10 );
    <fp group on the generators [ f1, f2, f3, f4, f5, f6, f7, f8, f9, f10,
    f11, f12, f13, f14, f15, f16, f17, f18, f19, f20, f21, f22, f23, f24,
    f25, f26 ]>
    gap> f2 := FreeGroup( "a", "b" );;
    gap> g := f2 / [ f2.1^625,
    > Comm(Comm(Comm(Comm(f2.2,f2.1),f2.1),f2.1),f2.1)/Comm(f2.2,f2.1)^5,
    > Comm(Comm(f2.2,f2.1),f2.2), f2.2^625 ];;
    gap> StandardPresentation( g, 5, "ClassBound", 15, "Metabelian" );
    <fp group on the generators [ f1, f2, f3, f4, f5, f6, f7, f8, f9, f10,
    f11, f12, f13, f14, f15, f16, f17, f18, f19, f20 ]>
    gap> f4 := FreeGroup( "a", "b", "c", "d" );;
    gap> g4 := f4 / [ f4.2^4, f4.2^2 / Comm(Comm (f4.2, f4.1), f4.1),
    >                 f4.4^16, f4.1^16 / (f4.3 * f4.4),
    >                 f4.2^8 / (f4.4 * f4.3^4) ];
    <fp group on the generators [ a, b, c, d ]>
    gap> g := Pq( g4, "Prime", 2, "ClassBound", 1 );
    <pc group of size 4 with 2 generators>
    gap> AutomorphismGroup(g);
    <group with 4 generators>
    gap> IsSolvable(last); 
    true
    gap> StandardPresentation(g4,g,"ClassBound",14,"PcgsAutomorphisms");
    <fp group with 53 generators>
\endexample

This function requires the package \"anupq\"\ (see `RequirePackage').

\>EpimorphismStandardPresentation( ... )

This function accepts the same parameters as the function
'StandardPresentation' and returns an epimorphism from the finitely
presnted group onto the finitely presented group given by a standard
presentation. 


Let  <G> be  a  $p$-group and  let <S>  be  the  standard presentation
computed for <G> by 'StandardPresentation'. 'IsomorphismPcpStandardPcp'
returns the isomorphism from <G> to <S>.

We illustrate the function with the following example.

\beginexample
    gap> F := FreeGroup (6);                                                
    <free group on the generators [ f1, f2, f3, f4, f5, f6 ]>
    gap> x := F.1;; y := F.2;; z := F.3;; w := F.4;; a := F.5;; b := F.6;;
    gap> R := [x^3 / w, y^3 / w * a^2 * b^2, w^3 / b,
    >              Comm (y, x) / z, Comm (z, x), Comm (z, y) / a, z^3 ];;
    gap> q := F / R;;
    gap> G := Pq (q, "Prime", 3, "ClassBound", 3);
    <pc group of size 729 with 6 generators>
    gap> phi := EpimorphismStandardPresentation(q, 3, "ClassBound", 3);
    [ f1, f2, f3, f4, f5, f6 ] -> [ f1*f2^2*f3*f4^2*f5^2, f1*f2*f3*f5,
    f3^2, f4*f6^2, f5, f6 ]
    gap> Size( Image(phi) );
    729
\endexample

This function requires the package \"anupq\"\ (see `RequirePackage').

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\Section{Testing p-Groups upon Isomorphism}

\>IsIsomorphicPGroup( <G>, <H> )

The functions returns true if <G> is isomorphic to <H>.  Both groups must
be pc groups of prime power order.

\beginexample
    gap> p1 := Group( (1,2,3,4), (1,3) );        
    Group([ (1,2,3,4), (1,3) ])
    gap> p1 := Image( IsomorphismPcGroup( p1 ) );
    Group([ f1, f2, f3 ])
    gap> p2 := SmallGroup( 8, 5 );
    <pc group of size 8 with 3 generators>
    gap> IsIsomorphicPGroup( p1, p2 );
    false
    gap> p3 := SmallGroup( 8, 4 );
    <pc group of size 8 with 3 generators>
    gap> IsIsomorphicPGroup( p1, p2 );
    false
    gap> p4 := SmallGroup( 8, 3 );   
    <pc group of size 8 with 3 generators>
    gap> IsIsomorphicPGroup( p1, p4 );
    true
\endexample

The function computes and compares the standard presentations for <G> and
<H> (see "StandardPresentation").

This function requires the package \"anupq\"\ (see `RequirePackage').

