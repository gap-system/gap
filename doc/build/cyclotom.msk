%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%
%A  cyclotom.msk                GAP documentation               Thomas Breuer
%A                                                           Alexander Hulpke
%%
%A  @(#)$Id$
%%
%Y  (C) 1998 School Math and Comp. Sci., University of St.  Andrews, Scotland
%Y  Copyright (C) 2002 The GAP Group
%%
%%  This file is being maintained by Thomas Breuer.
%%  Please do not make any changes without consulting him.
%%  (This holds also for minor changes such as the removal of whitespace or
%%  the correction of typos.)
%%
\Chapter{Cyclotomic Numbers}

\index{type!cyclotomic}\index{irrationalities}
\index{cyclotomic field elements}

{\GAP} admits computations in abelian extension fields of the rational
number field $\Q$, that is fields with abelian Galois group over $\Q$.
These fields are subfields of *cyclotomic fields* $\Q(e_n)$
where $e_n = e^{2\pi i/n}$ is a primitive complex $n$-th root of unity.
The elements of these fields are called *cyclotomics*.

Information concerning operations for domains of cyclotomics,
for example certain integral bases of fields of cyclotomics,
can be found in Chapter~"Abelian Number Fields".
For more general operations that take a field extension as a
---possibly optional--- argument, e.g., `Trace' or `Coefficients',
see Chapter~"Fields and Division Rings".


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\Section{Operations for Cyclotomics}

\index{roots of unity}

\Declaration{E}
\beginexample
gap> E(9); E(9)^3; E(6); E(12) / 3;
-E(9)^4-E(9)^7
E(3)
-E(3)^2
-1/3*E(12)^7
\endexample
A particular basis is used to express cyclotomics,
see~"Integral Bases of Abelian Number Fields";
note that `E(9)' is *not* a basis element,
as the above example shows.

\Declaration{Cyclotomics}
\beginexample
gap> E(9) in Cyclotomics; 37 in Cyclotomics; true in Cyclotomics;
true
true
false
\endexample

As the cyclotomics are field elements the usual arithmetic operators
`+',`-',`*' and `/' (and `^' to take powers by integers) are applicable.
Note that `^' does *not* denote the conjugation of group elements,
so it is *not* possible to explicitly construct groups of cyclotomics.
(However, it is possible to compute the inverse and the multiplicative
order of a nonzero cyclotomic.)
Also, taking the $k$-th power of a root of unity $z$ defines a Galois
automorphism if and only if $k$ is coprime to the conductor of $z$.

\beginexample
gap> E(5) + E(3); (E(5) + E(5)^4) ^ 2; E(5) / E(3); E(5) * E(3);
-E(15)^2-2*E(15)^8-E(15)^11-E(15)^13-E(15)^14
-2*E(5)-E(5)^2-E(5)^3-2*E(5)^4
E(15)^13
E(15)^8
gap> Order( E(5) ); Order( 1+E(5) );
5
infinity
\endexample

\Declaration{IsCyclotomic}
\beginexample
gap> IsCyclotomic(0); IsCyclotomic(1/2*E(3)); IsCyclotomic( infinity );
true
true
true
gap> IsCyc(0); IsCyc(1/2*E(3)); IsCyc( infinity );
true
true
false
\endexample

\Declaration{IsIntegralCyclotomic}
\beginexample
gap> r:= ER( 5 );                # The square root of 5 is a cyclotomic integer.
E(5)-E(5)^2-E(5)^3+E(5)^4
gap> IsIntegralCyclotomic( r );  # It has integral coefficients.
true
gap> r2:= 1/2 * r;               # This is not a cyclotomic integer, ...
1/2*E(5)-1/2*E(5)^2-1/2*E(5)^3+1/2*E(5)^4
gap> IsIntegralCyclotomic( r2 );
false
gap> r3:= 1/2 * r - 1/2;         # ... but this is one.
E(5)+E(5)^4
gap> IsIntegralCyclotomic( r3 );
true
\endexample

\indextt{Int!for cyclotomics}
The operation `Int'
can be used to find a cyclotomic integer near to an arbitrary cyclotomic.
For rationals, `Int' returns the largest integer smaller or equal to the
argument.
\beginexample
gap> Int( E(5)+1/2*E(5)^2 ); Int( 2/3*E(7)+3/2*E(4) );
E(5)
E(4)
\endexample

\indextt{String!for cyclotomics}
The operation `String'
returns for a cyclotomic a string corresponding to the way
the cyclotomic is printed by `ViewObj' and `PrintObj'.
\beginexample
gap> String( E(5)+1/2*E(5)^2 ); String( 17/3 );
"E(5)+1/2*E(5)^2"
"17/3"
\endexample

\Declaration{Conductor}
\beginexample
gap> Conductor( 0 ); Conductor( E(10) ); Conductor( E(12) );
1
5
12
\endexample

\Declaration{AbsoluteValue}
\beginexample
gap> AbsoluteValue(-3);
3
\endexample

\Declaration{RoundCyc}
\beginexample
gap> RoundCyc( E(5)+1/2*E(5)^2 ); RoundCyc( 2/3*E(7)+3/2*E(4) );
E(5)+E(5)^2
-2*E(28)^3+E(28)^4-2*E(28)^11-2*E(28)^15-2*E(28)^19-2*E(28)^23-2*E(28)^27
\endexample

\Declaration{CoeffsCyc}
\index{coefficients!for cyclotomics}
\beginexample
gap> cyc:= E(5)+E(5)^2;
E(5)+E(5)^2
gap> CoeffsCyc( cyc, 5 );  CoeffsCyc( cyc, 15 );  CoeffsCyc( cyc, 7 );
[ 0, 1, 1, 0, 0 ]
[ 0, -1, 0, 0, 0, 0, 0, 0, -1, 0, 0, -1, 0, -1, 0 ]
fail
\endexample

\Declaration{DenominatorCyc}

\>ExtRepOfObj( <cyc> )!{external representation!for cyclotomics}
\beginexample
gap> ExtRepOfObj( E(5) ); CoeffsCyc( E(5), 15 );
[ 0, 1, 0, 0, 0 ]
[ 0, 0, 0, 0, 0, 0, 0, 0, -1, 0, 0, 0, 0, -1, 0 ]
gap> CoeffsCyc( 1+E(3), 9 ); CoeffsCyc( E(5), 7 );
[ 0, 0, 0, 0, 0, 0, -1, 0, 0 ]
fail
\endexample

\Declaration{DescriptionOfRootOfUnity}
\beginexample
gap> E(9);  DescriptionOfRootOfUnity( E(9) );
-E(9)^4-E(9)^7
[ 9, 1 ]
gap> DescriptionOfRootOfUnity( -E(3) );
[ 6, 5 ]
\endexample

\Declaration{IsGaussInt}
\Declaration{IsGaussRat}


\indextt{DefaultField!for cyclotomics}
\FileHeader[1]{cyclotom}
\beginexample
gap> Field( E(5)+E(5)^4 );  DefaultField( E(5)+E(5)^4 );
NF(5,[ 1, 4 ])
CF(5)
\endexample


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\Section{Infinity}\nolabel

\Declaration{IsInfinity}
\beginexample
gap> s:= Size( Rationals );
infinity
gap> s = infinity; IsCyclotomic( s ); IsCyc( s ); IsInfinity( s );
true
true
false
true
gap> s in Rationals; s > 17;
false
true
gap> Set( [ s, 2, s, E(17), s, 19 ] );
[ 2, 19, E(17), infinity ]
\endexample


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\Section{Comparisons of Cyclotomics}

\index{operators!for cyclotomics}

To compare  cyclotomics, the operators `\<', `\<=', `=', `>=', `>' and
`\<>' can  be used, the result will be  `true' if the first operand is
smaller, smaller or equal, equal, larger or equal, larger, or unequal,
respectively, and `false' otherwise.

Cyclotomics are ordered as follows:
The relation between rationals is the natural one,
rationals are smaller than irrational cyclotomics,
and `infinity' is the largest cyclotomic.
For two irrational cyclotomics with different conductors,
the one with smaller conductor is regarded as smaller.
Two irrational cyclotomics with same conductor are compared via their
external representation.

For comparisons of cyclotomics and other {\GAP} objects,
see Section~"Comparisons".

\beginexample
gap> E(5) < E(6);      # the latter value has conductor 3
false
gap> E(3) < E(3)^2;    # both have conductor 3, compare the ext. repr.
false
gap> 3 < E(3); E(5) < E(7);
true
true
\endexample


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\Section{ATLAS Irrationalities}

\index{atomic irrationalities}

\atindex{b_N}{@$b\noexpand_N$}\atindex{c_N}{@$c\noexpand_N$}
\atindex{d_N}{@$d\noexpand_N$}\atindex{e_N}{@$e\noexpand_N$}
\atindex{f_N}{@$f\noexpand_N$}\atindex{g_N}{@$g\noexpand_N$}
\atindex{h_N}{@$h\noexpand_N$}
\Declaration{EB}
\atindex{i_N}{@$i\noexpand_N$}\atindex{r_N}{@$r\noexpand_N$}
\Declaration{EI}
\atindex{s_N}{@$s\noexpand_N$}\atindex{t_N}{@$t\noexpand_N$}
\atindex{u_N}{@$u\noexpand_N$}\atindex{v_N}{@$v\noexpand_N$}
\atindex{w_N}{@$w\noexpand_N$}\atindex{x_N}{@$x\noexpand_N$}
\atindex{y_N}{@$y\noexpand_N$}
\Declaration{EY}
\atindex{j_N}{@$j\noexpand_N$}\atindex{k_N}{@$k\noexpand_N$}
\atindex{l_N}{@$l\noexpand_N$}\atindex{m_N}{@$m\noexpand_N$}
\Declaration{EM}
\atindex{n_k}{@$n\noexpand_k$}
\Declaration{NK}
\Declaration{AtlasIrrationality}

\beginexample
gap> EW(16,3); EW(17,2); ER(3); EI(3); EY(5); EB(9);
0
E(17)+E(17)^4+E(17)^13+E(17)^16
-E(12)^7+E(12)^11
E(3)-E(3)^2
E(5)+E(5)^4
1
gap> AtlasIrrationality( "b7*3" );
E(7)^3+E(7)^5+E(7)^6
gap> AtlasIrrationality( "y'''24" );
E(24)-E(24)^19
gap> AtlasIrrationality( "-3y'''24*13&5" );
3*E(8)-3*E(8)^3
gap> AtlasIrrationality( "3y'''24*13-2&5" );
-3*E(24)-2*E(24)^11+2*E(24)^17+3*E(24)^19
gap> AtlasIrrationality( "3y'''24*13-&5" );
-3*E(24)-E(24)^11+E(24)^17+3*E(24)^19
gap> AtlasIrrationality( "3y'''24*13-4&5&7" );
-7*E(24)-4*E(24)^11+4*E(24)^17+7*E(24)^19
gap> AtlasIrrationality( "3y'''24&7" );
6*E(24)-6*E(24)^19
\endexample


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\Section{Galois Conjugacy of Cyclotomics}

\Declaration{GaloisCyc}
\Declaration{ComplexConjugate}
\beginexample
gap> GaloisCyc( E(5) + E(5)^4, 2 );
E(5)^2+E(5)^3
gap> GaloisCyc( E(5), -1 );           # the complex conjugate
E(5)^4
gap> GaloisCyc( E(5) + E(5)^4, -1 );  # this value is real
E(5)+E(5)^4
gap> GaloisCyc( E(15) + E(15)^4, 3 );
E(5)+E(5)^4
gap> ComplexConjugate( E(7) );
E(7)^6
\endexample

\Declaration{StarCyc}
\beginexample
gap> StarCyc( EB(5) ); StarCyc( E(5) );
E(5)^2+E(5)^3
fail
\endexample

\Declaration{Quadratic}
\beginexample
gap> Quadratic( EB(5) ); Quadratic( EB(27) );
rec( a := -1, b := 1, root := 5, d := 2, ATLAS := "b5", 
  display := "(-1+ER(5))/2" )
rec( a := -1, b := 3, root := -3, d := 2, ATLAS := "1+3b3", 
  display := "(-1+3*ER(-3))/2" )
gap> Quadratic(0); Quadratic( E(5) );
rec( a := 0, b := 0, root := 1, d := 1, ATLAS := "0", display := "0" )
fail
\endexample

\Declaration{GaloisMat}

In the following example we temporarily increase the line length limit from
its default value 80 to 84 in order to get a nicer output format.

\beginexample
gap> SizeScreen([ 84, ]);;
gap> GaloisMat( [ [ E(3), E(4) ] ] );
rec( 
  mat := [ [ E(3), E(4) ], [ E(3), -E(4) ], [ E(3)^2, E(4) ], [ E(3)^2, -E(4) ] ],
  galoisfams := [ [ [ 1, 2, 3, 4 ], [ 1, 7, 5, 11 ] ], 0, 0, 0 ], 
  generators := [ (1,2)(3,4), (1,3)(2,4) ] )
gap> SizeScreen([ 80, ]);;
gap> GaloisMat( [ [ 1, 1, 1 ], [ 1, E(3), E(3)^2 ] ] );
rec( mat := [ [ 1, 1, 1 ], [ 1, E(3), E(3)^2 ], [ 1, E(3)^2, E(3) ] ], 
  galoisfams := [ 1, [ [ 2, 3 ], [ 1, 2 ] ], 0 ], generators := [ (2,3) ] )
\endexample

\Declaration{RationalizedMat}
\beginexample
gap> mat:= [ [ 1, 1, 1 ], [ 1, E(3), E(3)^2 ], [ 1, E(3)^2, E(3) ] ];;
gap> RationalizedMat( mat );
[ [ 1, 1, 1 ], [ 2, -1, -1 ] ]
\endexample


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\Section{Internally Represented Cyclotomics}

The implementation of an *internally represented cyclotomic* is based on
a list of length equal to its conductor.
This means that the internal representation of a cyclotomic does *not*
refer to the smallest number field but the smallest *cyclotomic* field
containing it.
The reason for this is the wish to reflect the natural embedding of two
cyclotomic fields into a larger one that contains both.
With such embeddings, it is easy to construct the sum or the product
of two arbitrary cyclotomics (in possibly different fields) as an element
of a cyclotomic field.

The disadvantage of this approach is that the arithmetical operations
are quite expensive, so the use of internally represented cyclotomics
is not recommended for doing arithmetics over number fields, such as
calculations with matrices of cyclotomics.
But internally represented cyclotomics are good enough for dealing
with irrationalities in character tables (see chapter~"Character Tables").

For the representation of cyclotomics one has to recall that the
$n$-th cyclotomic field $\Q(e_n)$ is a vector space of dimension
$\varphi(n)$ over the rationals where $\varphi$ denotes Euler's
phi-function (see~"Phi").

A special integral basis of cyclotomic fields is chosen that allows one to
easily convert arbitrary sums of roots of unity into the
basis, as well as to convert  a cyclotomic represented w.r.t.~the basis
into the smallest possible cyclotomic field.
This basis is accessible in {\GAP},
see~"Integral Bases of Abelian Number Fields" for more information and
references.

Note that the set of all $n$-th roots of unity is linearly dependent
for $n > 1$, so multiplication is *not* the multiplication of the group
ring $\Q\langle e_n \rangle$;
given a $\Q$-basis of $\Q(e_n)$ the result of the multiplication
(computed as multiplication of polynomials in $e_n$,
using $(e_n)^n = 1$) will be converted to the basis.

\beginexample
gap> E(5) * E(5)^2; ( E(5) + E(5)^4 ) * E(5)^2;
E(5)^3
E(5)+E(5)^3
gap> ( E(5) + E(5)^4 ) * E(5);
-E(5)-E(5)^3-E(5)^4
\endexample

An internally represented cyclotomic is always represented in the smallest
cyclotomic field it is contained in.
The internal coefficients list coincides with the external representation
returned by `ExtRepOfObj'.

Since the conductor of internally represented cyclotomics must be
in the filter `IsSmallIntRep', the biggest possible (though not very
useful) conductor is $2^{28}-1$.
So the maximal cyclotomic field implemented in {\GAP} is not really
the field $\Q^{ab}$.

\beginexample
gap> IsSmallIntRep( 2^28-1 );
true
gap> IsSmallIntRep( 2^28 );
false
\endexample

It should be emphasized that one disadvantage of representing a cyclotomic in
the smallest *cyclotomic* field (and not in the smallest field) is that
arithmetic operations in a fixed small extension field of the rational
number field are comparatively expensive.
For example, take a prime integer $p$ and suppose that we want to work with
a matrix group over the field $\Q(\sqrt{p})$.
Then each matrix entry could be described by two rational coefficients,
whereas the representation in the smallest cyclotomic field requires
$p-1$ rational coefficients for each entry.
So it is worth thinking about using elements in a field constructed with
`AlgebraicExtension' (see~"AlgebraicExtension") when natural embeddings
of cyclotomic fields are not needed.

% show an example?


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%
%E

