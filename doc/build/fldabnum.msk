%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%
%A  fldabnum.msk                 GAP documentation              Thomas Breuer
%%
%A  @(#)$Id$
%%
%Y  (C) 1998 School Math and Comp. Sci., University of St.  Andrews, Scotland
%Y  Copyright (C) 2002 The GAP Group
%%
\Chapter{Abelian Number Fields}

\FileHeader[1]{fldabnum}


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\Section{Construction of Abelian Number Fields}

Besides the usual construction using `Field' or `DefaultField'
(see~"Operations for Abelian Number Fields"),
abelian number fields consisting of cyclotomics can be created with
`CyclotomicField' and `AbelianNumberField'.


\Declaration{CyclotomicField}

\beginexample
gap> CyclotomicField( 5 );  CyclotomicField( [ Sqrt(3) ] );
CF(5)
CF(12)
gap> CF( CF(3), 12 );  CF( CF(4), [ Sqrt(7) ] );
AsField( CF(3), CF(12) )
AsField( GaussianRationals, CF(28) )
\endexample


\Declaration{AbelianNumberField}

\beginexample
gap> NF( 7, [ 1 ] );
CF(7)
gap> f:= NF( 7, [ 1, 2 ] );  Sqrt(-7); Sqrt(-7) in f;
NF(7,[ 1, 2, 4 ])
E(7)+E(7)^2-E(7)^3+E(7)^4-E(7)^5-E(7)^6
true
\endexample


\Declaration{GaussianRationals}

\beginexample
gap> CF(4) = GaussianRationals;
true
gap> Sqrt(-1) in GaussianRationals;
true
\endexample

% factoring of elements in GaussianRationals works?


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\Section{Operations for Abelian Number Fields}

For operations for elements of abelian number fields, e.g.,
`Conductor' (see~"Conductor") or `ComplexConjugate' (see~"ComplexConjugate"),
see Chapter~"Cyclotomic Numbers".


\index{cyclotomics!DefaultField}
For a dense list $l$ of cyclotomics, `DefaultField' (see~"DefaultField")
returns the smallest cyclotomic field containing all entries of $l$,
`Field' (see~"Field") returns the smallest field containing all entries of
$l$, which need not be a cyclotomic field.
In both cases, the fields represent vector spaces over the rationals
(see~"Integral Bases of Abelian Number Fields").

\beginexample
gap> DefaultField( [ E(5) ] ); DefaultField( [ E(3), ER(6) ] );
CF(5)
CF(24)
gap> Field( [ E(5) ] ); Field( [ E(3), ER(6) ] );
CF(5)
NF(24,[ 1, 19 ])
\endexample


\index{polynomials over abelian number fields!Factors}
Factoring of polynomials over abelian number fields consisting of cyclotomics
works in principle but is not very efficient if the degree of the field
extension is large.

\beginexample
gap> x:= Indeterminate( CF(5) );
x_1
gap> Factors( PolynomialRing( Rationals ), x^5-1 );
[ x_1-1, x_1^4+x_1^3+x_1^2+x_1+1 ]
gap> Factors( PolynomialRing( CF(5) ), x^5-1 );
[ x_1-1, x_1+(-E(5)), x_1+(-E(5)^2), x_1+(-E(5)^3), x_1+(-E(5)^4) ]
\endexample


\Declaration{IsNumberField}
\Declaration{IsAbelianNumberField}
\Declaration{IsCyclotomicField}

\beginexample
gap> IsNumberField( CF(9) ); IsAbelianNumberField( Field( [ ER(3) ] ) );
true
true
gap> IsNumberField( GF(2) );
false
gap> IsCyclotomicField( CF(9) );
true
gap> IsCyclotomicField( Field( [ Sqrt(-3) ] ) );
true
gap> IsCyclotomicField( Field( [ Sqrt(3) ] ) );
false
\endexample


\Declaration{GaloisStabilizer}

\beginexample
gap> r5:= Sqrt(5);
E(5)-E(5)^2-E(5)^3+E(5)^4
gap> GaloisCyc( r5, 4 ) = r5;  GaloisCyc( r5, 2 ) = r5;
true
false
gap> GaloisStabilizer( Field( [ r5 ] ) );
[ 1, 4 ]
\endexample


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\Section{Integral Bases of Abelian Number Fields}

\FileHeader[2]{fldabnum}

\beginexample
gap> f:= CF(8);;   # a cycl. field over the rationals
gap> b:= CanonicalBasis( f );;  BasisVectors( b );
[ 1, E(8), E(4), E(8)^3 ]
gap> Coefficients( b, Sqrt(-2) );
[ 0, 1, 0, 1 ]
gap> f:= AsField( CF(4), CF(8) );;  # a cycl. field over a cycl. field
gap> b:= CanonicalBasis( f );;  BasisVectors( b );                    
[ 1, E(8) ]
gap> Coefficients( b, Sqrt(-2) );
[ 0, 1+E(4) ]
gap> f:= AsField( Field( [ Sqrt(-2) ] ), CF(8) );;
gap> # a cycl. field over a non-cycl. field
gap> b:= CanonicalBasis( f );;  BasisVectors( b );
[ 1/2+1/2*E(8)-1/2*E(8)^2-1/2*E(8)^3, 1/2-1/2*E(8)+1/2*E(8)^2+1/2*E(8)^3 ]
gap> Coefficients( b, Sqrt(-2) );
[ E(8)+E(8)^3, E(8)+E(8)^3 ]
gap> f:= Field( [ Sqrt(-2) ] );   # a non-cycl. field over the rationals
NF(8,[ 1, 3 ])
gap> b:= CanonicalBasis( f );;  BasisVectors( b );
[ 1, E(8)+E(8)^3 ]
gap> Coefficients( b, Sqrt(-2) );
[ 0, 1 ]
\endexample


\Declaration{ZumbroichBase}

\beginexample
gap> ZumbroichBase( 15, 1 ); ZumbroichBase( 12, 3 );
[ 1, 2, 4, 7, 8, 11, 13, 14 ]
[ 0, 3 ]
gap> ZumbroichBase( 10, 2 ); ZumbroichBase( 32, 4 );
[ 2, 4, 6, 8 ]
[ 0, 1, 2, 3, 4, 5, 6, 7 ]
\endexample


\Declaration{LenstraBase}

\beginexample
gap> LenstraBase( 24, [ 1, 19 ], [ 1, 19 ], 1 );
[ [ 1, 19 ], [ 8 ], [ 11, 17 ], [ 16 ] ]
gap> LenstraBase( 24, [ 1, 19 ], [ 1, 5, 19, 23 ], 1 );
[ [ 1, 19 ], [ 5, 23 ], [ 8 ], [ 16 ] ]
gap> LenstraBase( 15, [ 1, 4 ], PrimeResidues( 15 ), 1 );
[ [ 1, 4 ], [ 2, 8 ], [ 7, 13 ], [ 11, 14 ] ]
\endexample

The first two results describe two bases of the field $\Q_3(\sqrt{6})$,
the third result describes a normal basis of $\Q_3(\sqrt{5})$.

%T missing: `IsIntegralBasis', `NormalBasis', `IsNormalBasis',
%T rings of integers in abelian number fields


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\Section{Galois Groups of Abelian Number Fields}

\atindex{abelian number fields!Galois group}%
{@abelian number fields!Galois group}
\atindex{number fields!Galois group}{@number fields!Galois group}
\index{automorphism group!of number fields}

The field automorphisms of the cyclotomic field $\Q_n$
(see Chapter~"Cyclotomic Numbers") are given by the linear maps $\ast k$
on $\Q_n$ that are defined by $`E'(n)^{\ast k} = `E'(n)^k$,
where $1 \leq k \< n$ and $`Gcd'( n, k ) = 1$ hold (see~"GaloisCyc").
Note that this action is *not* equal to exponentiation of cyclotomics,
i.e., for general cyclotomics $z$, $z^{\ast k}$ is different from $z^k$.

(In {\GAP}, the image of a cyclotomic $z$ under $\ast k$ can be computed
as $`GaloisCyc'( z, k )$.)

\beginexample
gap> ( E(5) + E(5)^4 )^2; GaloisCyc( E(5) + E(5)^4, 2 );
-2*E(5)-E(5)^2-E(5)^3-2*E(5)^4
E(5)^2+E(5)^3
\endexample

For $`Gcd'( n, k ) \not= 1$, the map $`E'(n) \mapsto `E'(n)^k$ does *not*
define a field automorphism of $\Q_n$ but only a $\Q$-linear map.

\beginexample
gap> GaloisCyc( E(5)+E(5)^4, 5 ); GaloisCyc( ( E(5)+E(5)^4 )^2, 5 );
2
-6
\endexample

\Declaration{ANFAutomorphism}
\beginexample
gap> f:= CF(25);
CF(25)
gap> alpha:= ANFAutomorphism( f, 2 );
ANFAutomorphism( CF(25), 2 )
gap> alpha^2;
ANFAutomorphism( CF(25), 4 )
gap> Order( alpha );
20
gap> E(5)^alpha;
E(5)^2
\endexample

The Galois group $Gal( \Q_n, \Q )$ of the field extension
$\Q_n / \Q$ is isomorphic to the group $(\Z / n \Z)^{\ast}$
of prime residues modulo $n$, via the isomorphism
$(\Z / n \Z)^{\ast} \rightarrow Gal( \Q_n, \Q )$ that is defined by
$k + n \Z \mapsto ( z \mapsto z^{\ast k} )$.

The Galois group of the field extension $\Q_n / L$ with
any abelian number field $L \subseteq \Q_n$ is simply the
factor group of $Gal( \Q_n, \Q )$ modulo the stabilizer of $L$,
and the Galois group of $L / L^{\prime}$, with $L^{\prime}$ an abelian
number field contained in $L$, is the subgroup in this group that stabilizes
$L^{\prime}$.
These groups are easily described in terms of $(\Z / n \Z)^{\ast}$.
Generators of $(\Z / n \Z)^{\ast}$ can be computed using
`GeneratorsPrimeResidues' (see~"GeneratorsPrimeResidues").

In {\GAP}, a field extension $L / L^{\prime}$ is given by the field
object $L$ with `LeftActingDomain' value $L^{\prime}$
(see~"Integral Bases of Abelian Number Fields").

\beginexample
gap> f:= CF(15);
CF(15)
gap> g:= GaloisGroup( f );
<group with 2 generators>
gap> Size( g ); IsCyclic( g ); IsAbelian( g );
8
false
true
gap> Action( g, NormalBase( f ), OnPoints );
Group([ (1,6)(2,4)(3,8)(5,7), (1,4,3,7)(2,8,5,6) ])
\endexample

The following example shows Galois groups of a cyclotomic field
and of a proper subfield that is not a cyclotomic field.

\beginexample
gap> gens1:= GeneratorsOfGroup( GaloisGroup( CF(5) ) );
[ ANFAutomorphism( CF(5), 2 ) ]
gap> gens2:= GeneratorsOfGroup( GaloisGroup( Field( Sqrt(5) ) ) );
[ ANFAutomorphism( NF(5,[ 1, 4 ]), 2 ) ]
gap> Order( gens1[1] );  Order( gens2[1] );
4
2
gap> Sqrt(5)^gens1[1] = Sqrt(5)^gens2[1];
true
\endexample

The following example shows the Galois group of a cyclotomic field
over a non-cyclotomic field.

\beginexample
gap> g:= GaloisGroup( AsField( Field( [ Sqrt(5) ] ), CF(5) ) );
<group with 1 generators>
gap> gens:= GeneratorsOfGroup( g );
[ ANFAutomorphism( AsField( NF(5,[ 1, 4 ]), CF(5) ), 4 ) ]
gap> x:= last[1];;  x^2;
IdentityMapping( AsField( NF(5,[ 1, 4 ]), CF(5) ) )
\endexample


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\Section{Gaussians}

\Declaration{GaussianIntegers}
\Declaration{IsGaussianIntegers}

% Gcd and Euclidean... for the rings of integers in CF(4) and CF(3) !


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%
%E

