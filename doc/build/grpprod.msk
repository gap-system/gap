%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%
%A  grpprod.msk                 GAP documentation            Alexander Hulpke
%%
%A  @(#)$Id$
%%
%Y  (C) 1998 School Math and Comp. Sci., University of St.  Andrews, Scotland
%Y  Copyright (C) 2002 The GAP Group
%%
\Chapter{Group Products}

This chapter describes the various group product constructions that are
possible in {\GAP}.

At the moment for some of the products methods are available only if both
factors are given in the same representation or only for certain types of
groups such as  permutation groups and pc groups when the product can be
naturally represented as a group of the same kind.

{\GAP} does not guarantee that a product of two groups will be in a
particular representation.  (Exceptions are `WreathProductImprimitiveAction'
and `WreathProductProductAction' which are construction that makes sense
only for permutation groups, see~"WreathProduct").

{\GAP} however will try to choose an efficient representation, so products
of permutation groups or pc groups often will be represented as a group of
the same kind again.

Therefore the only guaranteed way to relate a product to its factors is via
the embedding and projection homomorphisms (see~"Embeddings and
Projections for Group Products");

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\Section{Direct Products}

The direct product of  groups is the cartesian product of the groups
(considered as element sets) with component-wise multiplication.

\Declaration{DirectProduct}

{\GAP} will try to choose an efficient representation for the direct
product. For example the direct product of permutation groups will be a
permutation group again and the direct product of pc groups will be a pc
group.

If the groups are in different representations a generic direct product will
be formed which may not be particularly efficient for many calculations.
Instead it may be worth to convert all factors to a common representation
first, before forming the product.

\atindex{Embedding!example for direct products}%
{@\noexpand`Embedding'!example for direct products}
\atindex{Projection!example for direct products}%
{@\noexpand`Projection'!example for direct products}
For a product <P> the operation `Embedding(<P>,<nr>)' returns the
homomorphism embedding the <nr>-th factor into <P>. The operation 
`Projection(<P>,<nr>)' gives the projection of <P> onto the <nr>-th factor
(see~"Embeddings and Projections for Group Products").

\beginexample
gap> g:=Group((1,2,3),(1,2));;
gap> d:=DirectProduct(g,g,g);
Group([ (1,2,3), (1,2), (4,5,6), (4,5), (7,8,9), (7,8) ])
gap> Size(d);
216
gap> e:=Embedding(d,2);
2nd embedding into Group([ (1,2,3), (1,2), (4,5,6), (4,5), (7,8,9), (7,8) ])
gap> Image(e,(1,2));
(4,5)
gap> Image(Projection(d,2),(1,2,3)(4,5)(8,9));
(1,2)
\endexample

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\Section{Semidirect Products}

The semidirect product of a group <N> with a group <G> acting on <N> via a
homomorphism $\alpha$ from <G> into the automorphism group of <N> is the
cartesian product $<G>\times<N>$ with the multiplication
$(g,n)\cdot(h,m)=(gh,n^{(h\alpha)}m)$.

\Declaration{SemidirectProduct}

\beginexample
gap> n:=AbelianGroup(IsPcGroup,[5,5]);
<pc group of size 25 with 2 generators>
gap> au:=DerivedSubgroup(AutomorphismGroup(n));;
gap> Size(au);
120
gap> p:=SemidirectProduct(au,n);
<permutation group with 5 generators>
gap> Size(p);
3000
\endexample

\beginexample
gap> n:=Group((1,2),(3,4));;
gap> au:=AutomorphismGroup(n);;
gap> au:=First(Elements(au),i->Order(i)=3);;
gap> au:=Group(au);
<group with 1 generators>
gap> SemidirectProduct(au,n);
Error, no method found! For debugging hints type ?Recovery from NoMethodFound
Error, no 2nd choice method found for `IsomorphismPcGroup' on 1 arguments
gap> IsGroupOfAutomorphisms(au);
true
gap> SemidirectProduct(au,n);
<pc group with 3 generators>
\endexample

\beginexample
gap> n:=AbelianGroup(IsPcGroup,[2,2]);
<pc group of size 4 with 2 generators>
gap> au:=AutomorphismGroup(n);
<group of size 6 with 2 generators>
gap> apc:=IsomorphismPcGroup(au);
CompositionMapping( Pcgs([ (2,3), (1,2,3) ]) -> 
[ f1, f2 ], <action isomorphism> )
gap> g:=Image(apc);
Group([ f1, f2 ])
gap> apci:=InverseGeneralMapping(apc);
 [ f1*f2^2, f1*f2 ] -> [ Pcgs([ f1, f2 ]) -> [ f1*f2, f2 ], 
   Pcgs([ f1, f2 ]) -> [ f2, f1 ] ]
gap> IsGroupHomomorphism(apci);
true
gap> p:=SemidirectProduct(g,apci,n);
<pc group of size 24 with 4 generators>
gap> IsomorphismGroups(p,Group((1,2,3,4),(1,2)));
[ f1, f2, f3, f4 ] -> [ (1,4), (1,4,3), (1,4)(2,3), (1,2)(3,4) ]
\endexample

\beginexample
gap> SemidirectProduct(SU(3,3),GF(9)^3);
<matrix group of size 4408992 with 3 generators>
gap> SemidirectProduct(Group((1,2,3),(2,3,4)),GF(5)^4);
<matrix group of size 7500 with 3 generators>
\endexample

\beginexample
gap> g:=Group((3,4,5),(1,2,3));;
gap> mats:=[[[Z(2^2),0*Z(2)],[0*Z(2),Z(2^2)^2]],
>          [[Z(2)^0,Z(2)^0], [Z(2)^0,0*Z(2)]]];;
gap> hom:=GroupHomomorphismByImages(g,Group(mats),[g.1,g.2],mats);;
gap> SemidirectProduct(g,hom,GF(4)^2);
<matrix group of size 960 with 3 generators>
gap> SemidirectProduct(g,hom,GF(16)^2);
<matrix group of size 15360 with 4 generators>
\endexample

\atindex{Embedding!example for semidirect products}%
{@\noexpand`Embedding'!example for semidirect products}
\atindex{Projection!example for semidirect products}%
{@\noexpand`Projection'!example for semidirect products}
For the semidirect product <P> of <G> with <N>, `Embedding(<P>,1)' embeds
<G>, `Embedding(<P>,2)' embeds <N>. The operation `Projection(<P>)' returns
the projection of <P> onto <G>
(see~"Embeddings and Projections for Group Products").
\beginexample
gap> Size(Image(Embedding(p,1)));
6
gap> Embedding(p,2);
[ f1, f2 ] -> [ f3, f4 ]
gap> Projection(p);
[ f1, f2, f3, f4 ] -> [ f1, f2, <identity> of ..., <identity> of ... ]
\endexample

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\Section{Subdirect Products}

The subdirect product of the groups $G$ and $H$ with respect to the
epimorphisms $\varphi\colon G\to A$ and $\psi\colon H\to A$ (for a common
group $A$) is the subgroup of the direct product $G\times H$ consisting of
the elements $(g,h)$ for which $g\varphi=h\psi$. It is the pull-back of the
diagram:
%display{tex}
$$
\matrix{
&&G&\cr
&&\Big\downarrow&\varphi\cr
H&\mathop{\longrightarrow}\limits^{\psi}&A&\cr
}
$$
%display{html}
%<PRE>
%                   G
%                   | phi
%             psi   V
%          H  --->  A
%</PRE>
%display{text}
%                   G
%                   | phi
%             psi   V
%          H  --->  A
%enddisplay

\Declaration{SubdirectProduct}

\atindex{Projection!example for subdirect products}%
{@\noexpand`Projection'!example for subdirect products}
For a subdirect product <P>, the operation `Projection(<P>,<nr>' returns
the projections on the <nr>-th factor. (In general the factors do not embed
in a subdirect product.)
\beginexample
gap> g:=Group((1,2,3),(1,2));
Group([ (1,2,3), (1,2) ])
gap> hom:=GroupHomomorphismByImagesNC(g,g,[(1,2,3),(1,2)],[(),(1,2)]);
[ (1,2,3), (1,2) ] -> [ (), (1,2) ]
gap> s:=SubdirectProduct(g,g,hom,hom);
Group([ (1,2,3), (1,2)(4,5), (4,5,6) ])
gap> Size(s);
18
gap> p:=Projection(s,2);
2nd projection of Group([ (1,2,3), (1,2)(4,5), (4,5,6) ])
gap> Image(p,(1,3,2)(4,5,6));
(1,2,3)
\endexample

\Declaration{SubdirectProducts}

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\Section{Wreath Products}

The wreath product of a group <G> with a permutation group <P> acting on <n>
points is the semidirect product of the normal subgroup $<G>^n$ with the
group <P> which acts on $<G>^n$ by permuting the components.

\Declaration{WreathProduct}

\atindex{Embedding!example for wreath products}%
{@\noexpand`Embedding'!example for wreath products}
\atindex{Projection!example for wreath products}%
{@\noexpand`Projection'!example for wreath products}
For a wreath product <W> of <G> with a permutation group <P> of degree <n>
and $1\le <nr>\le <n>$ the operation `Embedding(<W>,<nr>)' provides the
embedding of <G> in the <nr>-th component of the direct product of the base
group $<G>^n$ of <W>.
`Embedding(<W>,<n>+1)' is the embedding of <P> into <W>. The operation
`Projection(<W>)' provides the projection onto the acting group <P>
(see~"Embeddings and Projections for Group Products").
\beginexample
gap> g:=Group((1,2,3),(1,2));
Group([ (1,2,3), (1,2) ])
gap> p:=Group((1,2,3));
Group([ (1,2,3) ])
gap> w:=WreathProduct(g,p);
Group([ (1,2,3), (1,2), (4,5,6), (4,5), (7,8,9), (7,8), (1,4,7)(2,5,8)(3,6,9) 
 ])
gap> Size(w);
648
gap> Embedding(w,1);
1st embedding into Group( [ (1,2,3), (1,2), (4,5,6), (4,5), (7,8,9), (7,8), 
  (1,4,7)(2,5,8)(3,6,9) ] )
gap> Image(Embedding(w,3));
Group([ (7,8,9), (7,8) ])
gap> Image(Embedding(w,4));
Group([ (1,4,7)(2,5,8)(3,6,9) ])
gap> Image(Projection(w),(1,4,8,2,6,7,3,5,9));
(1,2,3)
\endexample

\Declaration{WreathProductImprimitiveAction}
\beginexample
gap> w:=WreathProductImprimitiveAction(g,p);;
gap> LargestMovedPoint(w);
9
\endexample

\Declaration{WreathProductProductAction}
\beginexample
gap> w:=WreathProductProductAction(g,p);
<permutation group of size 648 with 7 generators>
gap> LargestMovedPoint(w);
27
\endexample

\Declaration{KuKGenerators}
\beginexample
gap> g:=Group((1,2,3,4),(1,2));;
gap> hom:=GroupHomomorphismByImages(g,Group((1,2)),
> GeneratorsOfGroup(g),[(1,2),(1,2)]);;
gap> u:=PreImage(hom,Stabilizer(Image(hom),1));
Group([ (2,3,4), (1,2,4) ])
gap> hom2:=GroupHomomorphismByImages(u,Group((1,2,3)),
> GeneratorsOfGroup(u),[ (1,2,3), (1,2,3) ]);;
gap> KuKGenerators(g,hom,hom2);
[ (1,4)(2,5)(3,6), (1,6)(2,4)(3,5) ]
\endexample


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\Section{Free Products}

Let $G$ and $H$ be groups with presentations $\langle X\mid R\rangle$ and
$\langle Y\mid S\rangle$ respectively. Then the free product $G*H$ is
the group with presentation $\langle X\cup Y\mid R\cup S\rangle$. This
construction can be generalized to an arbitrary number of groups.

\Declaration{FreeProduct}

\beginexample
gap> g := DihedralGroup(8);;
gap> h := CyclicGroup(5);;
gap> fp := FreeProduct(g,h,h);
<fp group on the generators [ f1, f2, f3, f4, f5 ]>
gap> fp := FreeProduct([g,h,h]);
<fp group on the generators [ f1, f2, f3, f4, f5 ]>
gap> Embedding(fp,2);
[ f1 ] -> [ f4 ]
\endexample

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\Section{Embeddings and Projections for Group Products}

The relation between a group product and its factors is provided via
homomorphisms, the embeddings in the product and the projections from the
product. Depending on the kind of product only some of these are defined.

\>Embedding(<P>,<nr>)!{for group products} O

returns the <nr>-th embedding in the group product <P>. The actual meaning
of this embedding is described in the section for the appropriate product.

\>Projection(<P>[,<nr>])!{for group products} O

returns the (<nr>-th) projection of the group product <P>. The actual meaning
of the projection returned is described in the section for the appropriate
product.

