%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%
%W  records.tex                 GAP documentation               Thomas Breuer
%W                                                             & Frank Celler
%W                                                         & Martin Schoenert
%W							     & Heiko Theissen
%%
%H  @(#)$Id$
%%
%Y  Copyright 1997,    Lehrstuhl D fuer Mathematik,    RWTH Aachen,   Germany
%%
%%  This file contains a tutorial introduction to records.
%%


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\Chapter{Records}

A record provides another way to  build new data structures.  Like a list
a record is a collection of other  objects.  In a record the elements are
not indexed by numbers but by  names (i.e., identifiers).   An entry in a
record is called a *record component*.

In  the following sections  you  will see how to  define  and how to  use
records.  Record objects are changed by assignments to record fields.

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\Section{Plain Records}

Initially a record is defined as a comma separated list of assignments to
its record components.

\beginexample
    gap> date:= rec(year:= 1997,
    >               month:= "Jul",
    >               day:= 14);
    rec(
      year := 1997,
      month := "Jul",
      day := 14 )
\endexample

The value of a record component is accessible by  the record name and the
record  component name separated   by one dot   as  the record  component
selector.

\beginexample
    gap> date.year;
    1997
\endexample

Assignments to new record components  are possible in  the same way.  The
record is automatically resized to hold the new component.

\beginexample
    gap> date.time:= rec(hour:= 19, minute:= 23, second:= 12);
    rec(
      hour := 19,
      minute := 23,
      second := 12 )
    gap> date;
    rec(
      year := 1997,
      month := "Jul",
      day := 14,
      time := rec(
          hour := 19,
          minute := 23,
          second := 12 ) )
\endexample

Records are objects  that  may be  changed.   An assignment to  a  record
component  changes the original  object.    The remarks made in  Sections
"Identical Lists" and "Immutability" are also true for records.

Sometimes it is interesting to know which  components of a certain record
are  bound.  This information is available  from the function `RecNames',
which  takes a record as  its  argument and  returns  a list of all bound
components of this record as a list of strings.

\beginexample
    gap> RecNames(date);
    [ "year", "month", "day", "time" ]
\endexample

\exercise Finally try the following examples and explain the results.
\beginexample
    gap> r:= rec();
    rec(
       )
    gap> r:= rec(r:= r);
    rec(
      r := rec(
           ) )
    gap> r.r:= r;
    rec(
      r := ~ )
\endexample

\answer  The  first  assignment to `r'    creates a  record.   The second
assignment assigns a  new object to `r', namely  a record whose component
`r' contains the old record object created  in the first assignment.  The
last assignment changes the value of the record component `r' and creates
a recursive object.

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\Section{Further Reading}

Now return to Sections "Identical Lists"  and "Immutability" and find out
what that section means for records.

Records and functions  for records are  described in  detail  in  Chapter
"ref:Records".

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%
%E  records.tex	. . . . . . . . . . . . . . . . . . . . . . . . ends here
