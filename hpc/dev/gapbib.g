MRNumbersFromMathSciNet:=function()
#
# Retrieves from MathSciNet MR numbers of all publications citing GAP 
# We use "www.gap" in references to cover our old and new website addresses.
# This covers the majority of GAP citations. There might be several other 
# non-standard references, without website address or referring to the GAP
# manual. They mainly refer to early years and can be enumerated by hands.
#
local years, letters, allmrno, year, r, bib, mrno, b, letter, mrno1;
years := [ 1986 .. 2009 ]; 
letters := [ "A","B","C","D","E","F","G","H","I","J","K","L","M",
             "N","O","P","Q","R","S","T","U","V","W","X","Y","Z" ];
allmrno:=[];
for year in years do
  r:=SearchMR( rec( References:="www.gap", Year:=String(year) ) );
  if Length(r)<100 then
  
    bib:=ParseBibStrings(Concatenation(r))[1];
    mrno := List(bib, b -> b.Label );
    Print( year, " : ", Length(mrno), " citations \n");
    
  else
  
    mrno := [];
    Print( year, " : \c" );
    
    for letter in letters do
      r := SearchMR( rec( Author:=Concatenation(letter,"*"), References:="www.gap", Year:=String(year) ) );
      bib:=ParseBibStrings(Concatenation(r))[1];
      mrno1:= List(bib, b -> b.Label );
      Print( letter, "-", Length(mrno1), " \c");
      Append( mrno, mrno1 );
    od;
   
    mrno := Set( mrno );
    Print( "\n", year, " : ", Length(mrno), " citations \n");    

  fi;
  Append( allmrno, mrno );
od;
return Set(allmrno);
end;


MRNumbersFromGAP:=function()
#
# The function takes the BibTeX file and returns two lists:
# - for entries with MR number component, their MR number goes to the 
#   first list;
# - for entries without MR number component, GAP tries to search them
#   in MathSciNet. If the search is successful, then the pair with the
#   current label and MR number goes to the second list.
#
local bib, ignorelabels, mrno, guessno, b, nr, pos, search;
bib:=ParseBibFiles("~/CVSREPS/WWW2/Doc/Bib/gap-published.bib")[1];
Print( "Reading the GAP bibliography database ... \n" );
ignorelabels:=[ "phd:abughneim2005" ];
mrno := [];
guessno := [];
for b in bib do
  if IsBound(b.mrnumber) then
    nr := b.mrnumber;
    pos := PositionSublist( nr, " (" );
    if pos <> fail then
      nr := nr{[1..pos-1]};
    fi;
    Add( mrno, nr );
  else  
    search := SearchMRBib( b );
    if search <> [] and not b.Label in ignorelabels then
      bib:=ParseBibStrings(search[1])[1];  
      Print( b.Label, " --> ",  bib[1].Label, "\n" );
      Add( guessno, [ b.Label, bib[1].Label ]);
    fi;  
  fi;  
od;
mrno := Set( mrno );
Print("Check for duplicates in the GAP bibliography database returns ", 
      Filtered( guessno, b -> b[2] in mrno ), "\n" );
guessno := Set( Filtered( guessno, b -> not b[2] in mrno ) );
return [ mrno, guessno ];
end;


CheckMathSciNetForUpdates:=function()
#
# The function compares MR numbers from MathSciNet with known and suggested
# NR numbers for GAP citations database.
#
local ourdata, known, suggested, n, nr, mathrev, new;
ourdata := MRNumbersFromGAP();
known := ourdata[1];
suggested := ourdata[2];
Print( "GAP citations database : ", Length(known), 
       " known, ", Length(suggested), " suggested MR numbers \n" );
Print( "Saving suggestions to the file 'suggested.bib' ... \n" );       
PrintTo("suggested.bib", "Autogenerated by GAP\n");       
for nr in suggested do   
  Print( nr, "\n" );
  AppendTo( "suggested.bib", "% ", nr[1], "\n" );
  AppendTo( "suggested.bib", SearchMR( rec(MRNumber:=nr[2]))[1], "\n\n" );
od;    
mathrev := MRNumbersFromMathSciNet();
new := Difference( mathrev, Union( known, List( suggested, nr -> nr[2] ) ) );
Print( "MathSciNet   database  : ", Length(mathrev)-Length(new), 
       " known, ", Length(new), " new GAP citations \n" );
Print( "Saving new GAP citations the file 'tobeadded.bib' ... \n" );       
PrintTo("tobeadded.bib", "Autogenerated by GAP\n");       
for nr in new do   
  Print( nr, "\n" );
  AppendTo( "tobeadded.bib", "% ", nr, "\n" );
  AppendTo( "tobeadded.bib", SearchMR( rec(MRNumber:=nr))[1], "\n\n" );
od;  
Print("Check for updates completed! \n");
end;
